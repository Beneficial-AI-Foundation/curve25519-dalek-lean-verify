/-
Copyright (c) 2025 Beneficial AI Foundation. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Hoang Le Truong
-/
import Curve25519Dalek.Funs
import Curve25519Dalek.Math.Basic
import Curve25519Dalek.Specs.Backend.Serial.CurveModels.CompletedPoint.Add




/- # Spec Theorem for `CompletedPoint::sub`

Specification and proof for `CompletedPoint::sub`.

This function implements the mixed subtraction of an AffineNielsPoint from an
Edwards point in extended coordinates, returning the result in completed
coordinates (â„™Â¹ Ã— â„™Â¹). Given
- an EdwardsPoint P = (X:Y:Z:T) in extended â„™Â³ coordinates (with X/Z = x, Y/Z = y, and T = XY/Z),
- an AffineNielsPoint N = (Y+X, Yâˆ’X, 2dXY),
it computes a CompletedPoint C = (X':Y':Z':T') corresponding to P âˆ’ N.

The concrete formulas are:
- Y_plus_X  = Y + X
- Y_minus_X = Y âˆ’ X
- PM        = Y_plus_X  Â· N.y_minus_x
- MP        = Y_minus_X Â· N.y_plus_x
- Txy2d     = T Â· N.xy2d
- Z2        = Z + Z
- X'        = PM âˆ’ MP
- Y'        = PM + MP
- Z'        = Z2 âˆ’ Txy2d
- T'        = Z2 + Txy2d

**Source**: curve25519-dalek/src/backend/serial/curve_models/mod.rs
-/


open Aeneas.Std Result Aeneas.Std.WP
open curve25519_dalek.backend.serial.curve_models

namespace curve25519_dalek.Shared0EdwardsPoint.Insts.CoreOpsArithSubSharedAAffineNielsPointCompletedPoint



/-
natural language description:

â€¢ Takes an EdwardsPoint (X, Y, Z, T) in extended coordinates and an AffineNielsPoint
(Y+X, Yâˆ’X, 2dXY) and returns a CompletedPoint (X', Y', Z', T') in completed coordinates
(â„™Â¹ Ã— â„™Â¹), representing the group subtraction P âˆ’ N. Arithmetic is performed in the
field ğ”½_p where p = 2^255 - 19.

natural language specs:

â€¢ The function always succeeds (no panic)
â€¢ Given inputs P = (X, Y, Z, T) and N = (Y+X, Yâˆ’X, 2dXY), the output C = (X', Y', Z', T')
  satisfies modulo p:
  - X' â‰¡ ( (Y+X)Â·N.y_minus_x âˆ’ (Yâˆ’X)Â·N.y_plus_x ) (mod p)
  - Y' â‰¡ ( (Y+X)Â·N.y_minus_x + (Yâˆ’X)Â·N.y_plus_x ) (mod p)
  - Z' â‰¡ ( 2Â·Z âˆ’ TÂ·N.xy2d ) (mod p)
  - T' â‰¡ ( 2Â·Z + TÂ·N.xy2d ) (mod p)
-/

set_option maxHeartbeats 1000000 in
-- simp_all is heavy


@[progress]
theorem sub_spec
  (self : edwards.EdwardsPoint)
  (other : backend.serial.curve_models.AffineNielsPoint)
  (h_selfX_bounds : âˆ€ i, i < 5 â†’ (self.X[i]!).val < 2 ^ 53)
  (h_selfY_bounds : âˆ€ i, i < 5 â†’ (self.Y[i]!).val < 2 ^ 53)
  (h_selfZ_bounds : âˆ€ i, i < 5 â†’ (self.Z[i]!).val < 2 ^ 53)
  (h_selfT_bounds : âˆ€ i, i < 5 â†’ (self.T[i]!).val < 2 ^ 53)
  (h_otherYpX_bounds : âˆ€ i, i < 5 â†’ (other.y_plus_x[i]!).val < 2 ^ 53)
  (h_otherYmX_bounds : âˆ€ i, i < 5 â†’ (other.y_minus_x[i]!).val < 2 ^ 53)
  (h_otherXY2d_bounds : âˆ€ i, i < 5 â†’ (other.xy2d[i]!).val < 2 ^ 53) :
spec (Shared0EdwardsPoint.Insts.CoreOpsArithSubSharedAAffineNielsPointCompletedPoint.sub self other) (fun c =>
let X := Field51_as_Nat self.X
let Y := Field51_as_Nat self.Y
let Z := Field51_as_Nat self.Z
let T := Field51_as_Nat self.T
let YpX := Field51_as_Nat other.y_plus_x
let YmX := Field51_as_Nat other.y_minus_x
let XY2D := Field51_as_Nat other.xy2d
let X' := Field51_as_Nat c.X
let Y' := Field51_as_Nat c.Y
let Z' := Field51_as_Nat c.Z
let T' := Field51_as_Nat c.T
(X' + Y * YpX) % p = (((Y + X) * YmX) + X * YpX) % p âˆ§
(Y' + X * YpX) % p = (((Y + X) * YmX) + Y  * YpX) % p âˆ§
(Z' + (T * XY2D)) % p = (2 * Z) % p âˆ§
T' % p = ((2 * Z) + (T * XY2D)) % p)
:= by
  unfold Shared0EdwardsPoint.Insts.CoreOpsArithSubSharedAAffineNielsPointCompletedPoint.sub
  progress as âŸ¨Y_plus_X , h_Y_plus_X, Y_plus_X_bounds âŸ©
  progress as âŸ¨Y_minus_X,   Y_minus_X_bounds, h_Y_minus_XâŸ©
  Â· grind
  Â· grind
  progress  as âŸ¨ PM , h_PM , PM_boundsâŸ©
  Â· grind
  progress  as âŸ¨ MP, h_MP, MP_boundsâŸ©
  Â· grind
  Â· grind
  progress  as âŸ¨ Txy2d, h_Txy2d, Txy2d_boundsâŸ©
  Â· grind
  Â· grind
  progress as âŸ¨Z2, hZ2,  fZ2boundsâŸ©
  progress as âŸ¨fe1, h_fe1,  fe1_boundsâŸ©
  Â· grind
  Â· grind
  have hzz: âˆ€ i < 5, Z2[i]!.val < 2 ^ 54 := by simp_all
  obtain âŸ¨MPPM, h_MPPM_ok, h_MPPM, MPPM_boundsâŸ© := CompletedPoint.add_spec' hzz  Txy2d_bounds
  simp only [h_MPPM_ok, bind_tc_ok]
  progress as âŸ¨fe2, h_fe2, fe2_boundsâŸ©
  Â· grind
  Â· grind
  progress as âŸ¨fe3, h_fe3, fe3_boundsâŸ©
  Â· grind
  Â· grind
  constructor
  Â· rw[â† Nat.ModEq]
    rw[â† Nat.ModEq] at fe1_bounds
    have :  Field51_as_Nat self.Y + Field51_as_Nat self.X =Field51_as_Nat Y_plus_X := by
      simp[Field51_as_Nat, Finset.sum_range_succ ]
      simp_all
      scalar_tac
    rw[this]
    have := Nat.ModEq.mul_right (Field51_as_Nat other.y_plus_x) h_Y_minus_X
    have := Nat.ModEq.symm (Nat.ModEq.add_left (Field51_as_Nat fe1) this)
    rw[add_mul, â† add_assoc] at this
    apply Nat.ModEq.trans this
    apply Nat.ModEq.add_right
    apply  Nat.ModEq.symm
    apply Nat.ModEq.trans (Nat.ModEq.symm h_PM)
    apply Nat.ModEq.trans (Nat.ModEq.symm fe1_bounds)
    apply Nat.ModEq.add_left
    exact h_MP
  constructor
  Â· rw[â† Nat.ModEq]
    have :  Field51_as_Nat fe2 = Field51_as_Nat PM + Field51_as_Nat MP := by
      simp[Field51_as_Nat, Finset.sum_range_succ]
      simp_all
      scalar_tac
    rw[this]
    have := Nat.ModEq.add h_PM h_MP
    have := Nat.ModEq.add_right (Field51_as_Nat self.X * Field51_as_Nat other.y_plus_x) this
    apply Nat.ModEq.trans this
    have :  Field51_as_Nat self.Y + Field51_as_Nat self.X =Field51_as_Nat Y_plus_X := by
      simp[Field51_as_Nat, Finset.sum_range_succ ]
      simp_all
      scalar_tac
    rw[this, add_assoc]
    apply Nat.ModEq.add_left
    rw[â† add_mul]
    apply Nat.ModEq.mul_right
    rw[â† Nat.ModEq] at h_Y_minus_X
    exact h_Y_minus_X
  constructor
  Â· rw[â† Nat.ModEq]
    rw[â† Nat.ModEq] at fe3_bounds
    have :=  Nat.ModEq.add_left  (Field51_as_Nat fe3) h_Txy2d
    have := Nat.ModEq.trans (Nat.ModEq.symm this) fe3_bounds
    apply Nat.ModEq.trans this
    have :  Field51_as_Nat Z2 = Field51_as_Nat self.Z + Field51_as_Nat self.Z := by
      simp[Field51_as_Nat, Finset.sum_range_succ]
      simp_all
      scalar_tac
    rw[this, (by scalar_tac :âˆ€ a, a + a = 2 * a)]
  Â· rw[â† Nat.ModEq]
    have :  Field51_as_Nat MPPM = Field51_as_Nat Z2 + Field51_as_Nat Txy2d := by
      simp[Field51_as_Nat, Finset.sum_range_succ]
      simp_all
      scalar_tac
    rw[this]
    have :  Field51_as_Nat Z2 = Field51_as_Nat self.Z + Field51_as_Nat self.Z := by
      simp[Field51_as_Nat, Finset.sum_range_succ]
      simp_all
      scalar_tac
    simp[this, (by scalar_tac :âˆ€ a, a + a = 2 * a)]
    apply Nat.ModEq.add_left _ h_Txy2d

end curve25519_dalek.Shared0EdwardsPoint.Insts.CoreOpsArithSubSharedAAffineNielsPointCompletedPoint
