/-
Copyright (c) 2025 Beneficial AI Foundation. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Dablander
-/
import Curve25519Dalek.Aux
import Curve25519Dalek.Funs
import Curve25519Dalek.Defs
import Curve25519Dalek.Specs.Backend.Serial.U64.Field.FieldElement51.Pow2K

/-! # Spec Theorem for `FieldElement51::square2`

Specification and proof for `FieldElement51::square2`.

This function computes the square of the element and then doubles it.

**Source**: curve25519-dalek/src/backend/serial/u64/field.rs

## TODO
- Complete proof
-/

open Aeneas.Std Result

set_option linter.hashCommand false
#setup_aeneas_simps

namespace curve25519_dalek.backend.serial.u64.field.FieldElement51

/-
natural language description:

    ‚Ä¢ Computes twice the square of a field element a in the field ùîΩ_p where p = 2^255 - 19
    ‚Ä¢ The field element is represented as five u64 limbs

natural language specs:

    ‚Ä¢ The function always succeeds (no panic)
    ‚Ä¢ Field51_as_Nat(result) ‚â° 2 * Field51_as_Nat(a)¬≤ (mod p)
-/

/-- **Spec and proof concerning the loop in `backend.serial.u64.field.FieldElement51.square2`**:
- No panic when i ‚â§ 5
- Doubles each limb from index i onwards
- Leaves limbs before index i unchanged
-/
@[progress]
theorem square2_loop_spec (square : Array U64 5#usize) (i : Usize) (hi : i.val ‚â§ 5)
    (h_no_overflow : ‚àÄ (j : Nat), j < 5 ‚Üí i.val ‚â§ j ‚Üí square[j]!.val * 2 ‚â§ U64.max) :
    ‚àÉ r, square2_loop square i = ok r ‚àß
    (‚àÄ (j : Nat), j < 5 ‚Üí i.val ‚â§ j ‚Üí r[j]!.val = square[j]!.val * 2) ‚àß
    (‚àÄ (j : Nat), j < 5 ‚Üí j < i.val ‚Üí r[j]! = square[j]!) := by
  unfold square2_loop
  split
  ¬∑ progress*
    ¬∑ have := h_no_overflow i (by scalar_tac) (by simp)
      scalar_tac
    ¬∑ intro j hj _
      have := h_no_overflow j hj
      have := h_no_overflow j (by scalar_tac) (by omega)
      have : i.val ‚â† j := by scalar_tac
      simp_all
    ¬∑ refine ‚ü®?_, ?_‚ü©
      ¬∑ intro j hj _
        obtain hc | hc := (show j = i ‚à® i + 1 ‚â§ j by omega)
        ¬∑ simp_all
        ¬∑ have := res_post_1 j hj (by omega)
          have := Array.set_of_ne' square i2 j i (by scalar_tac) (by omega)
          have := Array.val_getElem!_eq' square j (by scalar_tac)
          simp_all
      ¬∑ intro j hj _
        have := res_post_2 j hj (by omega)
        simp_all
  ¬∑ use square
    simp only [implies_true, and_true, true_and]
    intro j hj _
    have : j = 5 := by scalar_tac
    omega
  termination_by 5 - i.val
  decreasing_by scalar_decr_tac

/-- **Spec and proof concerning `backend.serial.u64.field.FieldElement51.square2`**:
- No panic (always returns successfully)
- The result, when converted to a natural number, is congruent to twice the square of the input modulo p
-/
@[progress]
theorem square2_spec (a : Array U64 5#usize) :
    ‚àÉ r, square2 a = ok r ‚àß
    Field51_as_Nat r % p = (2 * (Field51_as_Nat a)^2) % p
    := by
  unfold square2
  progress*
  ¬∑ intro j hj _
    have := square_post_1 j hj
    scalar_tac
  ¬∑ have h_doubled : Field51_as_Nat res = 2 * Field51_as_Nat square := by
      unfold Field51_as_Nat
      rw [Finset.mul_sum]
      apply Finset.sum_congr rfl
      grind
    rw [Nat.ModEq] at square_post_2
    rw [h_doubled, Nat.mul_mod, square_post_2, ‚Üê Nat.mul_mod]

end curve25519_dalek.backend.serial.u64.field.FieldElement51
